(ns automata
  (:require [clojure.test :refer [deftest is run-tests]]))

(defrecord DFA [initial-state
                accept-states
                transitions])


(defn accepts?
  [{:keys [initial-state accept-states transitions]} input] ;keys recie all the keys
  (loop [input          input
         current-state  initial-state]
    (if (empty? input)
      (contains? accept-states current-state)
      (recur (rest input)
             ((transitions current-state) (first input))))))

(def dfa-1 (->DFA :q0   ;initial state
                  #{:q2}
                  {:q0 {\a :q1
                        \b :q0}
                   :q1 {\a :q1
                        \b :q2}
                   :q2 {\a :q2
                        \b :q2}})) ;} sigma

(accepts? dfa-1 "ab") ;=> true

(deftest test-problem1
  (is (accepts? dfa-1 "ab"))
  (is (accepts? dfa-1 "abba"))
  (is (accepts? dfa-1 "aaab"))
  (is (accepts? dfa-1 "abbbbbbbbb"))
  (is (not (accepts? dfa-1 "")))
  (is (not (accepts? dfa-1 "a")))
  (is (not (accepts? dfa-1 "baa")))
  (is (not (accepts? dfa-1 "bbba"))))

(run-tests)